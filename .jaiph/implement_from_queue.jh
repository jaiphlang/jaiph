#!/usr/bin/env jaiph

# Picks the first pending task from .jaiph/QUEUE.md, implements it, then removes it from the queue.
# Usage: ./implement.jh

# Verifies the queue file exists and has at least one pending task.
rule queue_has_tasks {
  test -f ".jaiph/QUEUE.md"
  grep -q "<!-- TASK id=" .jaiph/QUEUE.md
}

# Verifies the project compiles after changes.
rule project_compiles {
  npm run build
}

# Verifies tests pass after changes.
rule tests_pass {
  npm test
}

rule e2e_tests_pass {
  npm run test:e2e
}

# Reads the first task block from .jaiph/QUEUE.md and asks the agent to implement it.
workflow implement_task {
  prompt "
    You are working on the Jaiph codebase (https://github.com/jaiphlang/jaiph).
    The codebase is a TypeScript compiler and runtime for a DSL that transpiles to bash.
    Key source files: src/parser.ts, src/transpiler.ts, src/cli.ts

    Your job is to implement the FIRST task found in .jaiph/QUEUE.md.
    The task is the first block between <!-- TASK id=\"...\" --> and <!-- END_TASK -->.

    Steps:
    1. Read .jaiph/QUEUE.md and extract the first task block.
    2. Read the relevant source files listed under 'Files to change' in that task.
    3. Implement the changes described. Follow the existing code style exactly.
    4. Add the tests described under 'Tests to add'.
    5. Do not touch any other tasks or any files not listed in the task.
    6. Run npm test and npm run test:e2e.
    7. If tests fail, fix them and retry until both pass.
    8. Before finishing, make sure both unit/integration tests and e2e tests pass.

    When done, confirm which task ID you implemented and list every file you changed.
  "
}

# Removes the first completed task block from .jaiph/QUEUE.md.
workflow remove_completed_task {
  prompt "
    The first task in .jaiph/QUEUE.md has just been implemented successfully.

    Edit .jaiph/QUEUE.md to remove:
    - The first <!-- TASK id=\"...\" --> ... <!-- END_TASK --> block
    - The --- separator line that follows it (if present)

    Do not modify any other part of the file.
    Ensure you only remove the single first task block.
  "
}

workflow default {
  if ! ensure queue_has_tasks; then
    echo ".jaiph/QUEUE.md is empty or missing. Nothing to do."
    exit 0
  fi

  run implement_task
  ensure project_compiles

  if ! ensure tests_pass; then
    prompt "Fix failing tests so npm test passes, then re-run npm test and ensure it succeeds."
    ensure tests_pass
  fi

  if ! ensure e2e_tests_pass; then
    prompt "Fix failing e2e tests so npm run test:e2e passes, then re-run npm run test:e2e and ensure it succeeds."
    ensure e2e_tests_pass
  fi

  run remove_completed_task
}
